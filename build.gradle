// Top-level build file where you can add configuration options common to all sub-projects/modules.
group = 'it.gruppopam.android'
version = '1.0.17'
description = 'Android library for store apps'

buildscript {

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.24.14"
        classpath "org.stoyicker.android-check:plugin:2.292"
//        classpath "com.chaitanyapramod.gradle:findbugs-android:1.0"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

configurations {
    generic {
        description = 'generic'
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}

def libraryVersionAlreadyExistes = true;

task resolveCompileSources {
    doLast {
        try {
            configurations.getByName("generic").incoming.resolutionResult.allDependencies.collect { it.selected.id }

            libraryVersionAlreadyExistes = true;
            println "Library version already exists!"
        } catch (Exception e) {
            libraryVersionAlreadyExistes = false;
            println "Library not exists"
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: "com.jfrog.artifactory"
apply from: "$rootProject.projectDir/quality.gradle"

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments clearPackageData: 'true'

        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "artifacts/store-app-library-${version}-${variant.name}.aar"
        }

    }

    afterEvaluate {
        apply plugin: 'maven-publish'

        artifactoryPublish.skip = true

        publishing {
            artifactoryPublish.dependsOn('resolveCompileSources')
            artifactoryPublish.dependsOn('assembleRelease')
            artifactoryPublish.dependsOn('generatePomFileForAarPublication')

            publications {
                // Creates a Maven publication called "aar".
                aar(MavenPublication) {
                    // Applies the component for the release build variant.
                    artifact("build/outputs/aar/artifacts/store-app-library-${version}-release.aar")
                }
            }
        }

        artifactoryPublish {
            /*doFirst {
                if (libraryVersionAlreadyExistes)
                    throw new GradleException("Library version already exists!")
            }*/
            publications(publishing.publications.aar)
        }

        artifactory {
            publish {
                contextUrl = System.getenv("ARTIFACTORY_URL")
                repository {
                    repoKey = System.getenv("MAVEN_REPO_KEY") //The Artifactory repository key to publish to

                    username = System.getenv("ARTIFACTORY_USER")          //The publisher user name
                    password = System.getenv("ARTIFACTORY_PASS")
                }

                defaults {
                    publications ('aar')
                    publishBuildInfo = true   //Publish build-info to Artifactory (true by default)
                    publishArtifacts = true   //Publish artifacts to Artifactory (true by default)
                    publishPom = true   //Publish generated POM files to Artifactory (true by default).
                    publishIvy = true   //Publish generated Ivy descriptor files to Artifactory (true by default).
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mavenLocal().getUrl())
        }
    }
}

uploadArchives.dependsOn(build)

apply from: 'dependencies.gradle'

dependencies {
    generic "it.gruppopam.android:${rootProject.name}:${version}"
}